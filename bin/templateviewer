#!/usr/bin/env perl
use strict;
use warnings;

use Getopt::Long qw(GetOptionsFromArray :config gnu_getopt auto_version);
use Pod::Usage;
use Path::Class;
use Cwd;
use Carp;
use Data::Dumper;

use App::TemplateViewer;

use Plack::Builder;
use Plack::Runner;
use version 0.77; our $VERSION = qv("v0.1.0");

# main
if ( $ENV{DEBUG} ) {
    $Carp::Verbose = 1;
}

my @plackup_args = (); #TODO : ugry
my $args = parse_args(@ARGV) or exit 0;

if ( $args->{init} ) {
    App::TemplateViewer->init;
    exit 0;
}
elsif ( $args->{sync} ) {
    App::TemplateViewer::FileWatcher->new( watch => [ $args->{target} ] )->run;
}


# setup plack app
my $app = App::TemplateViewer->run($args);
{
    if ( exists $args->{static} ) {
        $app = build_static_path( $app, $args->{static} );
    }
    if ( exists $args->{builder} ) {
        $app = builder {
            do $args->{builder};
            $app;
        }
    }
}

my $runner = Plack::Runner->new;
$runner->parse_options(@plackup_args);
$runner->run($app);

# subroutine

sub build_static_path {
    my ( $app, $h ) = @_;
    while ( my ( $k, $v ) = each %$h ) {
        $app = Plack::Middleware::Static->wrap( $app, path => qr($k), root => $v );
    }
    $app;
}

sub parse_args {
    my @args = @_;
    my @args_pattern
        = ( 'help|h', 'format|f=s', 'builder|b=s', 'config|c=s', 'data|d=s', 'target|t=s', 'port|p=i', 'static|s=s%', 'w=s', 'init', 'sync!' );
    my %h = (
        format => 'tt2',
        sync   => 1,
    );
    if ( not GetOptionsFromArray( \@args, \%h, @args_pattern ) ) { pod2usage(2); return; }
    if ( exists $h{help} ) { pod2usage(1); return; }
    if ( not App::TemplateViewer->is_supported_format( $h{format} ) ) {
        printf STDERR "%s is not supported\n", $h{format};
        printf STDERR "supported type : %s\n", join q{ }, App::TemplateViewer->supported_format;
        return;
    }

    # load config
    my %cfg = {};
    if ( exists $h{config} ) {
        _check_file($h{config}) or return;
        %cfg = %{do delete $h{config}};
    }

    # set default for template variables
    {
        if ( not exists $cfg{tv} ) {
            $cfg{tv} = {};
        }
        map{$cfg{tv}{$_} ||= +{} } App::TemplateViewer->supported_format;
    }

    # overwrite config by cui args
    while (my ($k, $v) = each %h) {
        $cfg{$k} = $v;
    }

    if ( exists $cfg{builder} ) {
        _check_file($cfg{builder}) or return;
    }
    if ( exists $cfg{static} and 'HASH' eq ref $cfg{static} ) {
        while ( my ( $k, $v ) = each %{ $cfg{static} } ) {
            $v = _check_dir($v) or return;
        }
    }
    $cfg{target} ||= getcwd();
    $cfg{target} = _check_dir($cfg{target}) or return;

    if ( @args > 0 ) {
        @plackup_args = @args;
    }

    # TODO temp
    $cfg{data} ||= '/tmp/templateviewer';

    # output config file
    if ( exists $cfg{w} ) {
        my $file = delete $cfg{w};
        $Data::Dumper::Terse = 1;
        open my $fh, '>', $file or die $!;
        print $fh '+' . Dumper \%cfg;
        close $fh;
    }

    # set plackup_args
    foreach my $k (qw(port)) {
        if ( exists $cfg{$k} ) {
            push @plackup_args, "--$k", delete $cfg{$k};
        }
    }

    return \%cfg;
}

sub _check_dir {
    my $dir = shift;
    if ( not -d $dir ) {
        print STDERR "$dir is not directory";
        return;
    }
    return  dir($dir)->absolute->resolve;
}

sub _check_file {
    my $file = shift;
    if ( not -f $file ) {
        print STDERR "$file is not found";
        return;
    }
    return file($file)->absolute->resolve;
}


__END__

=pod

=head1 NAME

templateviewer - template viewer

=head1 SYNOPSIS

  templateviewer [options] [ -- [plackup options] ]

=head1 DESCRIPTION

templateviewer is template viewer

=head1 OPTIONS

=over 8

=item B<--target|-t> /path/to/target

set target directory. ( default: current directory )

=item B<--data|-d> /path/to/data/dir

set data directory. ( default: /tmp/templateviewer/ )

=item B<--format|-f> format

set default format type. ( default: tt2 )

=item B<--[no]sync> 

sync on/off switch.
if sync is on (default), on updating target file, auto-update textarea and preview.

=item B<--port|p> port_number

this option is passed to plackup

=item B<--static|s> path_info=path_to_file [--static path_info2=path_to_file2

this option can set multiple args.
this option path to Plack::Middleware::Static like

  Plack::Middleware::Static->wrap($app, path => qr(path_info), root => 'path_to_file');

=item B<--config|c> config_file

config file for template viewer. At first, recommend to use 'w' option.
config format like bellow.

    +{
        [option_name] => value

        tv => {
            [template format] => {

            },
        },
    }

tv hash ref is handed to template constructor as array (like %{$h->{type}})

=item B<-w> config_output_file

output config file from command line option.

=item B<--builer|b> builder_file
 
builder file for set static file url. this file will do in builder block.

    #!perl
    use Plack::Builder;
    use File::Basename;
    use File::Spec;
    enable "Plack::Middleware::Static",
      path => qr{^/images|js|css|static/}, root => File::Spec->catfile(dirname(__FILE__), 'htdocs');

=item B<--help|-h>

show help

=item B<--version>

show version

=item B<--> plack options

you can pass options after B<--> to plackup

=back

=head1 AUTHOR

ywatase E<lt>ywatase@gmail.comE<gt>

=head1 SEE ALSO

=head1 LICENSE

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=cut

# vim: set ft=perl:
